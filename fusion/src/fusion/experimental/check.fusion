// Copyright (c) 2012-2018 Amazon.com, Inc.  All rights reserved.

/*                       *** WARNING ***
 *
 * There are customer dependencies on this module!  See FUSION-333
 */

(module check "/fusion"

  '''
A lightweight unit testing library.

This module allows applications to build a suite of tests in the form of Fusion
scripts, where each script uses this module to express a number of _checks_
that each validate some aspect of the code under test.

For examples of these features in action, peruse
[Fusion's own test suite](code:trees/mainline/--/ftst).

These APIs are inspired by those of
[RackUnit](http://docs.racket-lang.org/rackunit)
by Noel Welsh and Ryan Culpepper.

<!-- FUSION-185 This comment forces FusionDoc to stop the one-liner here. -->

## WARNING

This module contains unstable, experimental features.
There is **NO SUPPORT** for this module.


## Checks

_Checks_ are the fundamental components of test scripts.  A check executes some
logic, and either returns void to express success, or throws
a "check exception" to express failure.
This library provides a range of primitive checks, and applications can
compose them into higher-level checks with broader responsibilites.

In order to present helpful output when failures occur, all checks are
syntactic forms.  Nevertheless, they work like procedures: the arguments are
treated as expressions and evaluated left-to-right.  (They cannot, however, be
used like first-class procedures and passed as runtime values.)


## Expecting Exceptions

[Exceptions](fusion/exception.html) can be tested using several `expect_*`
forms, verifing that certain kinds of values
are `raise`d by some code under test.  Unlike the `check_*` forms, these do not
act like procedures: evaluation of the expression is implicitly delayed via
`thunk` to allow an exception handler to be installed.

The expect forms observe the hierarchy of exception types.
For example, `expect_contract_error` will succeed whenever `expect_arity_error`
would succeed.


## Defining New Checks

You can compose your own checks from those given here by using `define_check`.
By doing so, failure reports will include the check name, source code location,
and actual arguments.  The entire active "stack" of checks is reported.

Failure of a check is induced via `fail_check` which raises a check exception
that displays the current stack of check frames.

Here's some examples from the basic checks:

    (define_check (fail)
      (fail_check))

    (define_check (check op val1 val2)
      (unless (op val1 val2)
        (fail_check)))

    (define_check (check_pred pred val)
      (unless (pred val)
        (fail_check)))

    (define_check (check_same expected actual)
      (unless (same expected actual)
        (fail_check)))

For simple cases like these, there's `define_simple_check` and
`define_binary_check`; the latter improves failure reporting by displaying
the expected and actual values.

    (define_simple_check (check_pred pred val)
      (pred val))

    (define_binary_check (check_same expected actual)
      (same expected actual))
  '''


  (require
    "/fusion/eval"
    "/fusion/exception"
    "/fusion/experimental/defines"
    "/fusion/experimental/syntax"
    "/fusion/ffi/java"
    "/fusion/parameter")


  (define check_stack_param
    (make_parameter null))



  (define CheckExn (java_class "com.amazon.fusion.CheckException"))

  (defpub (is_check_error v)
    '''
Determines whether a value is a `check_error` exception, a subtype of
`error_exn`.  Returns `true` or `false`.
    '''
    (java_instance_of CheckExn v))


  (defpub fail_check
    '''
Raises a `check _error`, using the current context stack as populated by the
dynamically enclosing checks.
    '''
    (java_new "com.amazon.fusion.FailCheckProc" check_stack_param))


  // Not public: the signature is awkward.
  (define (_fail_check meta)
    '''
Raises a `check _error`, including a stack trace derived from the
`check_stack_param` marks.
    '''
    (parameterize [(check_stack_param meta)]
      (fail_check)))


  (defpub_syntax define_check
    '''
    (define_check (name arg ...) expr ...)

Defines a new check form that's invoked as:

    (name arg_expr ... [message])

Note that the optional `message` arg is added automatically and is not part of
the declared signature.

The body is typically composed of several other checks, all of which must pass
for the new check to succeed.  The body may not use the new check recursively.
Any results from the body are ignored and the check form always returns void.

For example:

    (define_check (check_comparisons lo hi)
      (check <  lo hi)
      (check <= lo hi)
      (check =  lo lo)
      (check =  hi hi)
      (check >= hi lo)
      (check >  hi lo))
    '''
    (let [(ctx   (quote_syntax here)),
          (wrong (lambda (stx)
                   (wrong_syntax stx
                     '''Bad syntax for define_check; usage: '''
                     '''(define_check (name arg ...) expr ...)''')))]
      (lambda (stx)
        (when (< (syntax_size stx) 3)
          (wrong stx))
        (lets
          [(orig_args (tail (syntax_unwrap stx))),
           (signature (let [(s (syntax_unwrap (head orig_args)))]
                        (if (and (is_sexp s)
                                 (> (size s) 0)
                                 (every is_identifier s))
                            s
                            (wrong stx)))),
           (usage     (string_append "("
                                     (apply string_append
                                       (map (lambda (i)
                                              (string_append
                                                (syntax_unwrap i)
                                                " "))
                                          signature))
                                     "[message])")),
           (docs      (let [(d (syntax_unwrap (element orig_args 1)))]
                        (if (is_string d) d null.string))),
           (body      (if (is_null docs)
                          (tail orig_args)
                          (tail (tail orig_args)))),
           (name_stx  (head signature)),
           // TODO check for duplicate arg names
           (args      (tail signature)),
           (arg_count (size args)),
           (arity_msg (string_append (syntax_unwrap name_stx)
                                     " requires "
                                     (int_to_string arg_count)
                                     " or "
                                     (int_to_string (+ 1 arg_count))
                                     (if (= 1 arg_count) " arg" " args")
                                     "; usage: "
                                     usage)),
           (proc_name (datum_to_syntax
                          (string_to_symbol
                            (string_append "_" (syntax_unwrap name_stx)))
                          // TODO FUSION-329 Should use stx as context.
                          // This isn't always correct since the name could
                          // be coming from elsewhere.
                          name_stx))]
          (datum_to_syntax
            (quasiquote
              (begin
                (define (unquote proc_name)
                  (lambda rest  // (meta ARG ... [message])
                    // Arity checking has happened in the calling check macro.
                    (let [(named_args (subseq rest
                                              1
                                              (unquote (+ 1 arg_count))))]
                      (parameterize
                        [(check_stack_param
                           (struct_merge (head rest)
                             (apply struct
                               "name" (quote (unquote name_stx))
                               "args" named_args
                               (if (> (size rest) (unquote (+ 1 arg_count)))
                                   (sexp "message" (last rest))
                                   (quote ())))))]
                        (apply (lambda (unquote args)
                                 (unquote (pair (quote begin) body)))
                          named_args)
                        // All checks return void.
                        (void)))))
                (define_syntax (unquote name_stx)
                  (unquote (string_append "\n    " usage "\n"
                             (or docs "")))
                  (let [(ctx1 (quote_syntax here1))]
                    (lambda (stx1)
                      (let [(actuals (tail (syntax_unwrap stx1)))]
                        (when (or (< (size actuals) (unquote arg_count))
                                  (> (size actuals) (unquote (+ 1 arg_count))))
                          (wrong_syntax stx1 (unquote arity_msg)))
                        (let [(meta { expression: (syntax_to_datum stx1),
                                      line:       (syntax_line stx1),
                                      column:     (syntax_column stx1) })]
                          (datum_to_syntax
                            (pair (quote_syntax (unquote proc_name))
                                  (pair (sexp (quote quote) meta)
                                        actuals))
                            ctx1
                            stx1))))))))
            ctx
            stx)))))


  (defpub_syntax define_simple_check
    '''
    (define_simple_check (name arg ...) expr ...)

Defines a new check form that's invoked as:

    (name arg_expr ... [message])

Note that the optional `message` arg is added automatically and is not part of
the declared signature.

The new check succeeds if the result of the `expr`s is truthy.
The body may not use the new check recursively.

For example:

    (define_simple_check (check_pred pred val)
      (pred val))
    '''
    (let [(ctx (quote_syntax here)),
          (wrong (lambda (stx)
                   (wrong_syntax stx
                     '''Bad syntax for define_simple_check; usage: '''
                     '''(define_simple_check (name arg ...) expr ...)''')))]
      (lambda (stx)
        (when (< (syntax_size stx) 3)
          (wrong stx))
        (lets
          [(orig_args (tail (syntax_unwrap stx))),
           (signature (let [(s (syntax_unwrap (head orig_args)))]
                        (if (and (is_sexp s)
                                 (> (size s) 0)
                                 (every is_identifier s))
                            s
                            (wrong stx)))),
           (docs      (let [(d (syntax_unwrap (element orig_args 1)))]
                        (if (is_string d) d null.string))),
           (body      (if (is_null docs)
                          (tail orig_args)
                          (tail (tail orig_args))))]
          (datum_to_syntax
            (quasiquote
              (define_check (unquote signature)
                (unquote docs)
                (if (unquote (pair (quote begin) body))
                    (void)
                    (fail_check))))
            ctx
            stx)))))


  (defpub_syntax define_binary_check
    '''
    (define_binary_check (name expected actual) expr ...)

Defines a new check form that's invoked with expected and actual values, and an
optional message.

Like `define_simple_check`, the new check succeeds if the result of the `expr`s
is truthy. However, this form automatically adds the `expected` and `actual`
values to the failure report.

The body may not use the new check recursively.

For example:

    (define_binary_check (check_same expected actual)
      (same expected actual))
    '''
    (let [(ctx (quote_syntax here)),
          (wrong (lambda (stx)
                   (wrong_syntax stx
                     '''Bad syntax for define_binary_check; usage: '''
                     '''(define_binary_check (name expected actual) expr ...)''')))]
      (lambda (stx)
        (when (< (syntax_size stx) 3)
          (wrong stx))
        (lets
          [(orig_args (tail (syntax_unwrap stx))),
           (signature (let [(s (syntax_unwrap (head orig_args)))]
                        (if (and (is_sexp s)
                                 (=== (size s) 3)
                                 (every is_identifier s))
                            s
                            (wrong stx)))),
           (docs      (let [(d (syntax_unwrap (element orig_args 1)))]
                        (if (is_string d) d null.string))),
           (body      (if (is_null docs)
                          (tail orig_args)
                          (tail (tail orig_args))))]
          (datum_to_syntax
            (quasiquote
              (define_check (unquote signature)
                (unquote docs)
                (parameterize
                  [(check_stack_param
                      (struct "expected" (unquote (element signature 1))
                              "actual"   (unquote (element signature 2))))]
                  (if (unquote (pair (quote begin) body))
                      (void)
                      (fail_check)))))
            ctx
            stx)))))


  //===========================================================================

  (provide fail)
  (define_check (fail)
    '''
A check that fails unconditionally.
    '''
    (fail_check))


  (provide check)
  (define_simple_check (check op val1 val2)
    '''
Checks that the result of `(op val1 val2)` is truthy.
    '''
    (op val1 val2))


  (provide check_pred)
  (define_simple_check (check_pred pred val)
    '''
Checks that the result of `(pred val)` is truthy.
    '''
    (pred val))


  (provide check_same)
  (define_binary_check (check_same expected actual)
    '''
Checks that `(same expected actual)` is truthy.
    '''
    (same expected actual))


  (provide check_true)
  (define_check (check_true val)
    '''
Checks that `val` is `true` (as opposed to truthy).
Annotations are ignored.
    '''
    (unless (= true val)
      (_fail_check {actual:val})))


  (provide check_false)
  (define_check (check_false val)
    '''
Checks that `val` is `false` (as opposed to untruthy).
Annotations are ignored.
    '''
    (unless (= false val)
      (_fail_check {actual:val})))


  (provide check_null)
  (define_check (check_null val)
    '''
Checks that `val` is any null.
    '''
    (unless (is_null val)
      (_fail_check {actual:val})))


  (provide check_void)
  (define_check (check_void val)
    '''
Checks that `val` is void.
    '''
    (unless (is_void val)
      (_fail_check {actual:val})))


  (provide check_annotations)
  (define_check (check_annotations expected value)
    '''
Checks that the `value` has the `expected` annotations.  `expected` must be a
sequence of strings or symbols.

For example, these checks succeed:

    (check_annotations [] null)
    (check_annotations ["a"] (quote a::null))
    (check_annotations (quote (a b)) (quote a::b::null))

This one fails because the expected and actual annotations are not in the same
order:

    (check_annotations ["b", "a"] (quote a::b::null))
    '''
    (unless (= expected (annotations value))
      (_fail_check {expected:expected, actual:(annotations value)})))


  //===========================================================================

  // TODO This should just use `display`. : {{{arity_exn}}}
  (define (describe_raised v)
    (cond
      ((is_argument_error v) "argument_error")
      ((is_arity_error    v) "arity_error")
      ((is_result_error   v) "result_error")
      ((is_contract_error v) "contract_error")
      ((is_check_error    v) "check_error")
      ((is_unbound_error  v) "unbound_error")
      ((is_syntax_error   v) "syntax_error")
      ((is_error_exn      v) "error_exn")
      ((is_exn            v) "exn")
      (true v)))

  (define (_expect_raise pred expected thunk)
    (let [(meta (with_handlers
                  [(pred void), // If predicate matches, handler returns void.
                   ((|e| true)  // For anything else, describe what was raised.
                    (|e| { actual: (describe_raised e) }))]
                  (thunk)
                  // The thunk was expected to raise, so the check has failed.
                  { actual: "no exception raised" }))]
      (when meta (_fail_check (if expected
                                (put meta "expected" expected)
                                meta)))))


  // This should really be a procedure, but it doesn't work.
  (define_syntax define_exn_check
    '''
    (define_exn_check name pred type_name)

Defines a exception-expecting form used like `(name EXPR)`.
    '''
    (lambda (stx)
      (lets [(ctx (quote_syntax here)),
             (name_stx (syntax_get stx 1)),
             (pred_stx (syntax_get stx 2)),
             (type_name (syntax_unwrap (syntax_get stx 3))),
             (skip_frame (not type_name)),
             (docs (if skip_frame ""
                     (string_append '''
    (''' (syntax_unwrap name_stx) ''' expr [message])

Evaluates `expr` and checks that it throws ''' type_name '''.
If no exception is thrown, or if
another kind of exception is thrown, then the check fails.''')))]
        (datum_to_syntax
          (quasiquote
            (define_syntax (unquote name_stx)  // (name EXPR [message])
              (unquote docs)
              (let [(ctx1 (quote_syntax here1))]
                (lambda (stx1)
                  (lets [(actuals (tail (syntax_unwrap stx1))),
                         (thunk_stx (if (or (is_empty actuals)
                                            (> (size actuals) 2))
                                        (wrong_syntax stx1
                                          (unquote
                                            (string_append
                                              (syntax_unwrap name_stx)
                                              " requires 1 or 2 args; usage: ("
                                              (syntax_unwrap name_stx)
                                              " expr [message])")))
                                        (head actuals))),
                         (meta (unquote
                                 (if skip_frame {}
                                   (quasiquote
                                     (struct "name"       (quote (unquote name_stx))
                                             "expression" (syntax_to_datum stx1)
                                             "line"       (syntax_line stx1)
                                             "column"     (syntax_column stx1))))))]
                    (datum_to_syntax
                        // (parameterize
                        //   [(check_stack_param META)]
                        //   (_expect_raise PRED TYPE_NAME (lambda () BODY)))
                      (sexp
                        (quote_syntax parameterize)
                        [(sexp (quote_syntax check_stack_param)
                               (if (== (syntax_size stx1) 2)
                                 (sexp (quote_syntax quote) meta)
                                 (quasisyntax
                                   (struct_merge
                                     (quote (unsyntax (datum_to_syntax meta)))
                                     (struct "message"
                                       (unsyntax (syntax_get stx1 2)))))))]
                        (sexp
                          (quote_syntax _expect_raise)
                          (quote_syntax (unquote pred_stx))
                          (unquote type_name)
                          (sexp (quote_syntax lambda) (quote ()) thunk_stx)))
                      ctx1
                      stx1))))))
          ctx
          stx))))


  (provide
    expect_any_raise
    expect_argument_error
    expect_arity_error
    expect_check_error
    expect_contract_error
    expect_error_exn
    expect_exn
    expect_result_error
  )

  (define_exn_check expect_any_raise      (always true)     "any value")
  (define_exn_check expect_exn            is_exn            "exn")
  (define_exn_check expect_error_exn      is_error_exn      "error_exn")
  (define_exn_check expect_argument_error is_argument_error "argument_error")
  (define_exn_check expect_arity_error    is_arity_error    "arity_error")
  (define_exn_check expect_check_error    is_check_error    "check_error")
  (define_exn_check expect_contract_error is_contract_error "contract_error")
  (define_exn_check expect_result_error   is_result_error   "result_error")

  // NOT PUBLIC! Only used by the public version below.
  (define_exn_check _expect_syntax_exn  is_syntax_error   false /*skip frame*/)

  (defpub_syntax expect_syntax_error
    '''
    (expect_syntax_error top_form [message])

Passes the given top-level form to `eval` and checks that it throws a
`syntax_error` exception (as thrown by `wrong_syntax`).

This form currently doesn't work as expected within a module: the evaluation
context is not the module's namespace, because `current_namespace` isn't
changed when compiling a module's body.  To test for syntax errors in a module,
wrap the module with this form. So don't do this:

    (module test "/fusion"
      (expect_syntax_error <something>))

but do this instead:

    (expect_syntax_error
      (module test "/fusion"
        <something>))
    '''
    (lambda (stx)
      (let [(actuals (tail (syntax_unwrap stx)))]
        (when (or (is_empty actuals) (> (size actuals) 2))
          (wrong_syntax stx
            '''expect_syntax_error requires 1 or 2 args; '''
            '''usage: (expect_syntax_error top_form [message])'''))
        (let [(top_form (head actuals)),
              (meta (struct "name"       (quote expect_syntax_error)
                            "expression" (syntax_to_datum stx)
                            "line"       (syntax_line stx)
                            "column"     (syntax_column stx)
                            "expected"   (quote syntax_error)))]
          (datum_to_syntax
            // (parameterize
            //   [(check_stack_param (quote META))]
            //   (_expect_syntax_exn (eval (quote_syntax TOP)) [MESSAGE]))
            (sexp
              (quote_syntax parameterize)
              [(sexp (quote_syntax check_stack_param)
                     (sexp (quote_syntax quote) meta))]
              (apply sexp
                (quote_syntax _expect_syntax_exn)
                (sexp (quote_syntax eval)
                  (sexp (quote_syntax quote_syntax) top_form))
                (tail actuals)))
            (quote_syntax here)
            stx)))))


  //===========================================================================
  // Provide deprecated bindings for backwards compatibility.

  (define_syntax defpub_old_expect
    (lambda (stx)
      (lets
        [(args (tail (syntax_unwrap stx))),
         (old_name (head args)),
         (new_name (head (tail args))),
         (docs (string_append
                 "DEPRECATED.  Renamed to `"
                 (syntax_unwrap new_name)
                 "`."))]
        (datum_to_syntax
          (sexp (quote_syntax defpub_syntax) old_name
            docs
            new_name)
          (quote_syntax here)
          stx))))

  (defpub_old_expect expect_any_exn      expect_any_raise)
  (defpub_old_expect expect_argument_exn expect_argument_error)
  (defpub_old_expect expect_arity_exn    expect_arity_error)
  (defpub_old_expect expect_check_exn    expect_check_error)
  (defpub_old_expect expect_contract_exn expect_contract_error)
  (defpub_old_expect expect_result_exn   expect_result_error)
  (defpub_old_expect expect_syntax_exn   expect_syntax_error)

)
